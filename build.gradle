plugins {
    // project plugins
    id 'java-gradle-plugin'
    id 'groovy'
    // test coverage
    id 'jacoco'

    // ide plugin
    id 'idea'

    // intershop test plugin
    id 'com.intershop.gradle.test' version '2.2.0'

    // plugin for documentation
    id 'org.asciidoctor.convert' version '1.6.1'

    // publish plugin
    id 'maven-publish'

    // artifact signing - necessary on Maven Central
    id 'signing'

    // intershop version plugin
    id 'com.intershop.gradle.scmversion' version '2.3.4'

    // plugin for publishing to Gradle Portal
    id 'com.gradle.plugin-publish' version '0.10.0'
}

scm {
    version {
        initialVersion = '1.0.0'
    }
}

// release configuration
group 'com.intershop.gradle.buildinfo'
description 'Gradle Buildinfo Plugin'
version = scm.version.version

sourceCompatibility = 1.7
targetCompatibility = 1.7

// set correct project status
if (project.version.endsWith('-SNAPSHOT')) {
    status = 'snapshot'
}

// test configuration
test {
    if(System.getenv('SVNUSER') && System.getenv('SVNPASSWD') && System.getenv('SVNURL')) {
        systemProperty 'svnurl', System.getenv('SVNURL')
        systemProperty 'svnuser', System.getenv('SVNUSER')
        systemProperty 'svnpasswd', System.getenv('SVNPASSWD')
    }
    if(System.getenv('GITUSER') && System.getenv('GITPASSWD') && System.getenv('GITURL')) {
        systemProperty 'giturl', System.getenv('GITURL')
        systemProperty 'gituser', System.getenv('GITUSER')
        systemProperty 'gitpasswd', System.getenv('GITPASSWD')
    }

    if(System.getProperty('SVNUSER') && System.getProperty('SVNPASSWD') && System.getProperty('SVNURL')) {
        systemProperty 'svnurl', System.getProperty('SVNURL')
        systemProperty 'svnuser', System.getProperty('SVNUSER')
        systemProperty 'svnpasswd', System.getProperty('SVNPASSWD')
    }

    if(System.getProperty('GITUSER') && System.getProperty('GITPASSWD') && System.getProperty('GITURL')) {
        systemProperty 'giturl', System.getProperty('GITURL')
        systemProperty 'gituser', System.getProperty('GITUSER')
        systemProperty 'gitpasswd', System.getProperty('GITPASSWD')
    }

    //Change directory for gradle tests
    systemProperty 'org.gradle.native.dir', '.gradle'
    //Set supported Gradle version
    systemProperty 'intershop.gradle.versions', '2.11'
}

task copyAsciiDoc {
    def outputDir = file("$buildDir/tmp/asciidoctorSrc")
    def inputFiles = fileTree(dir: rootDir, include: '**/*.asciidoc')

    inputs.files inputFiles
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()

        copy {
            from inputFiles
            into outputDir
        }
    }
}

asciidoctorj {
    noDefaultRepositories = true
}

asciidoctor {
    sourceDir "${buildDir}/tmp/asciidoctorSrc"
    sources {
        include 'README.asciidoc'
    }

    backends 'html5', 'docbook'
    options doctype : 'article',
            ruby    : 'erubis'
    attributes  'latestRevision'     : project.version,
            'toc':'left',
            'toclevels':'2',
            'source-highlighter' : 'coderay',
            'icons': 'font',
            'setanchors':'true',
            'idprefix':'asciidoc_',
            'idseparator':'-',
            'docinfo1':'true'
}

asciidoctor.dependsOn copyAsciiDoc
jar.dependsOn asciidoctor

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task groovydocJar(type: Jar) {
    dependsOn groovydoc
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

// additional sources artifact
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

configurations {
    documentation
}

artifacts {
    archives jar
    archives sourceJar
    archives groovydocJar
    documentation new File(buildDir, 'asciidoc/html5/README.html')
    documentation new File(buildDir, 'asciidoc/docbook/README.xml')
}

publishing {
    publications {
        intershopMvn(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact groovydocJar {
                classifier "javadoc"
            }

            artifact(new File(buildDir, 'asciidoc/html5/README.html')) {
                classifier 'reference'
            }

            artifact(new File(buildDir, 'asciidoc/docbook/README.xml')) {
                classifier = 'docbook'
            }

            pom.withXml {
                def root = asNode();
                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                root.appendNode('url', "https:/gitlab.intershop.de/IntershopGradlePlugins/${project.name}")

                def organisation = root.appendNode( 'organization' )
                organisation.appendNode("name", "Intershop Communications AG")
                organisation.appendNode("url", "http://intershop.com")

                def scm = root.appendNode( 'scm' )
                scm.appendNode( 'url', "https://github.com/IntershopCommunicationsAG/${project.name}")
                scm.appendNode( 'connection', "git@github.com:IntershopCommunicationsAG/${project.name}.git")
                scm.appendNode( 'developerConnection', "git@github.com:IntershopCommunicationsAG/${project.name}.git")

                def license = root.appendNode( 'licenses' ).appendNode( 'license' )
                license.appendNode( 'name', 'Apache License, Version 2.0' )
                license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode( 'distribution', 'repo' )

                def developer = root.appendNode('developers').appendNode( 'developer' )
                developer.appendNode( 'id', 'm-raab' )
                developer.appendNode( 'name', 'M. Raab' )
                developer.appendNode( 'email', 'mraab@intershop.de' )

                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.(jar)\.asc$/
                    def ref_matcher = it.file =~ /\.html\.asc$/
                    def doc_matcher = it.file =~ /\.xml\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                        extension = "jar.asc"
                    } else {
                        if(ref_matcher.find()) {
                            classifier = 'reference'
                            extension = 'html.asc'
                        }
                        else if(doc_matcher.find()) {
                            classifier = 'docbook'
                            extension = 'xml.asc'
                        } else {
                            classifier = null
                            extension = 'jar.asc'
                        }
                    }

                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = uri(version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl)
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign configurations.archives, configurations.documentation
}

publish.dependsOn asciidoctor

pluginBundle {
    website = "https://github.com/IntershopCommunicationsAG/${project.name}"
    vcsUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"
    description = project.description
    tags = ['intershop', 'gradle', 'plugin', 'test']

    plugins {
        gradleTestPlugin {
            id = 'com.intershop.gradle.buildinfo'
            displayName = project.description
        }
    }
}

dependencies {
    //svn
    // replace svnkit's JNA 4.x with 3.2.7, which is used by Gradle itself
    compile('org.tmatesoft.svnkit:svnkit:1.8.14') {
        exclude group: 'net.java.dev.jna'
        exclude group: 'com.trilead', module: 'trilead-ssh2'
    }
    compile 'com.trilead:trilead-ssh2:1.0.0-build220'
    runtime 'net.java.dev.jna:jna:3.2.7'

    //jgit
    compile('org.eclipse.jgit:org.eclipse.jgit:4.5.0.201609210915-r') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

model {
    tasks.generatePomFileForIntershopMvnPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishIntershopMvnPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishIntershopMvnPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}
